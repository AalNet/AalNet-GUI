"Name"     = 'TAPAAL Query Language'
"Author"   = 'Lasse Jacobsen'
"Version"  = '1.0'
"About"    = 'TAPAAL Query Language is the language used for specifying queries in the verification tool TAPAAL.'
"Case Sensitive" = 'false'
"Start Symbol" = <AbstractProperty>

! Note that the grammar is constructed using the GOLD Parser Builder (http://www.devincook.com/goldparser/).
! GOLD Parser Builder is also used to create skeleton files for the java parser for the grammar.
! At the time of writing the GOLD Parser Builder is only available for Windows, 
! however, the Java code produced for the parser should work on any platform
! ------------------------------------------------- Sets

{WS}           = {Whitespace} - {CR} - {LF}
{ID Head}      = {Letter} + [_]                     
{ID Tail}      = {Alphanumeric} + [_]


! ------------------------------------------------- Terminals
 
Identifier    = {ID Head}{ID Tail}*
Num     = {Number}+  


! ------------------------------------------------- Rules


<AbstractProperty>    ::= 'EF' <Expr> | 'E<>' <Expr> | 'EG' <Expr> | 'E[]' <Expr> | 'AF' <Expr> | 'A<>' <Expr> | 'AG' <Expr> | 'A[]' <Expr>

<Expr>    ::= <Or>
    
<Or>      ::= <Or> 'or' <And> | <Or> '||' <And> | <And>

<And>     ::=  <And> 'and' <Not> | <And> '&&' <Not> | <Not>

<Not>     ::= 'not' '(' <Expr> ')'  | '!' '(' <Expr> ')' | <Factor>

<Factor>      ::=  <AtomicProposition1> | <AtomicProposition2> | '(' <Expr> ')'

<AtomicProposition1>   ::=  Identifier '<' Num | Identifier '<=' Num | Identifier '=' Num | Identifier '==' Num | Identifier '>=' Num | Identifier '>' Num
<AtomicProposition2>   ::=   Identifier '.' <AtomicProposition1>






